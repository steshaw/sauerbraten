// standard menu definitions
// don't modify, add personal menus to autoexec.cfg instead

bind ESCAPE [if (cleargui 1) [] [showgui main]]    // it all starts here

newgui main [
    guibutton "multiplayer.."    "showgui multiplayer"
    guibutton "singleplayer.."   "showgui singleplayer"
    guibutton "load map.."       "showgui maps"
    guibutton "editing.."        "showgui editing"
    guibutton "options.."        "showgui options"
    guibutton "about.."          "showgui about"
    guibutton "quit"             "quit"                 "exit"
]

newgui about [
    guitext "Cube 2: Sauerbraten"
    guitext [by Wouter "Aardappel" van Oortmerssen, Lee "Eihrul" Salzman,]
    guitext [Mike "Gilt" Dysart, Robert "a-baby-rabbit" Pointon,]
    guitext [John "geartrooper" Siar, and others]
    guitext "(for a full list of contributors see the readme)"
    guitext "http://sauerbraten.org/" 
]

maprotation = [
    n = (listlen $arg1)
    loop i $n [
        curmap = (at $arg1 $i)
        next = (mod (+ $i 1) $n)
        nextmap = (at $arg1 $next)
        (if (= $numargs 2) [concatword nextmap_ $arg2 $curmap] [concatword nextmap_ $curmap]) = $nextmap
    ]
]
    
genmapitems = [
    n = (listlen $arg1)
    loop i $n [
        curmap = (at $arg1 $i)
        guibutton $curmap (concat map $curmap) "cube"
    ]
]

mainmaps = "metl4 deathtek fanatic_quake aard3c metl2 ruby curvedm metl3 nmp8 complex douze killfactory lostinspace oasis aqueducts corruption thor"
moremaps1 = "academy tartech refuge kffa kalking1 orbe wake5 ot fragplaza hog2 roughinery shadowed DM_BS1 shindou moonlite darkdeath thetowers"
moremaps2 = "konkuri-to stemple tejen ogrosupply frostbyte fanatic_castle_trap nmp10 island neondevastation neonpanic orion katrez_d ksauer1 pgdm oddworld phosgene sdm1"
moremaps3 = "kmap5 serpentine battleofthenile guacamole hades paradigm mechanic wdcd"
capturemaps1 = "river_c paradigm fb_capture urban_c serenity nevil_c lostinspace face-capture nmp9 c_valley nmp4 nmp8 fc3 ph-capture monastery corruption hades"
capturemaps2 = "asteroids venice relic frostbyte ogrosupply hallo reissen akroseum duomo capture_night c_egypt tejen fc4 dust2 campo killcore3 damnation"
ctfmaps1 = "hallo reissen dust2 berlin_wall shipwreck akroseum flagstone face-capture valhalla urban_c damnation mach2 redemption tejen europium capture_night l_ctf"
ctfmaps2 = "forge sctf1 paradiselost campo wdcd"
conceptmaps = "ladder spiralz canyon secondevermap firstevermap"

maprotation (concat $mainmaps $moremaps1 $moremaps2 $moremaps3)
maprotation (concat $capturemaps1 $capturemaps2) capture_
maprotation (concat $ctfmaps1 $ctfmaps2) ctf_

alias showmapshot [ guibar; guiimage (concatword "packages/base/" (if (> $numargs 0) [result $arg1] [result (at $guirollovername 0)]) ".jpg") $guirolloveraction 4 1 "data/cube.png"]

newgui maps [
    guilist [
      guilist [ genmapitems $mainmaps ]
      showmapshot
    ]
    guitab 2 
    guilist [
      guilist [ genmapitems $moremaps1 ]
      guilist [ genmapitems $moremaps2 ]
      showmapshot
    ]
    guitab 3
    guilist [
      guilist [ genmapitems $moremaps3 ]
      showmapshot
    ]
    guitab capture
    guilist [
      guilist [ genmapitems $capturemaps1 ]
      guilist [ genmapitems $capturemaps2 ]
      showmapshot
    ]
    guitab ctf
    guilist [
        guilist [ genmapitems $ctfmaps1 ]
        guilist [ genmapitems $ctfmaps2 ]
        showmapshot
    ]

    guitab concept
    guilist [
        guilist [ genmapitems $conceptmaps ]
        showmapshot
    ]
]

newgui capturemaps [
    guilist [
      guilist [ genmapitems $capturemaps1 ]
      guilist [ genmapitems $capturemaps2 ]
      showmapshot
    ]
]

newgui ctfmaps [
    guilist [
        guilist [ genmapitems $ctfmaps1 ]
        guilist [ genmapitems $ctfmaps2 ]
        showmapshot
    ]
]

newgui multiplayer [
    guilist [
        guitext "name: " (at "mrfixit ironsnout ogro" $playermodel)
        newname = (getname)
        guifield newname 15 [name $newname]
    ]
    guibutton "server browser.."  "showgui servers"
    guibutton "vote game mode / map.." "showgui gamemode"
    // guibutton "lanconnect"
    // guibutton "connect localhost"
    guibutton "disconnect"
    guibutton "switch team" [if (strcmp (getteam) "good") [team evil] [team good]]
]

newgui singleplayer [
    guibutton "Start Private Stan Sauer"           "showgui privatestansauer"
    guibutton "Start An Army Of One"               "showgui armyofone"
    guibutton "start DMSP map.."                   "mode -2; showgui maps"
    guibutton "start slowmotion DMSP map.."        "mode -4; showgui maps"
    guitext   "skill (default: 3)"
    guislider skill
]

newgui armyofone [
    guilist [
        guilist [
            guibutton "Part I" "sp mpsp6a" "cube"
            guibutton "Part II" "sp mpsp6b" "cube"
            guibutton "Part III" "sp mpsp6c" "cube"
        ]
        showmapshot (substr $guirolloveraction 3)
    ]
] "An Army Of One"

newgui privatestansauer [
    guilist [
        guilist [
            guibutton "Run N' Gun Part I" "sp mpsp9a" "cube"
            guibutton "Run N' Gun Part II" "sp mpsp9b" "cube"
            guibutton "Run N' Gun Part III" "sp mpsp9c" "cube"
            guibutton "THE SERIOUSLY BIG VALLEY" "sp mpsp10" "cube"
        ]
        showmapshot (substr $guirolloveraction 3)
    ]
] "Private Stan Sauer"

newgui servers [
    guistayopen [
        guilist [
            guibutton "update server list from master server" "updatefrommaster"
            guibar
            guicheckbox "search LAN" searchlan
        ]
    ]
    guibar
    guiservers
]

newgui gamemode [
    guilist [
        guilist [
            guibutton "ffa"                "mode 0; showgui maps"
            guibutton "coop-edit"          "mode 1; showgui maps"
            guibutton "teamplay"           "mode 2; showgui maps"
            guibutton "instagib"           "mode 3; showgui maps"
            guibutton "instagib team"      "mode 4; showgui maps"
            guibutton "efficiency"         "mode 5; showgui maps"
            guibutton "efficiency team"    "mode 6; showgui maps"
            guibutton "tactics"            "mode 7; showgui maps"
            guibutton "tactics team"       "mode 8; showgui maps"
        ]
        guibar
        guilist [
            guibutton "capture"            "mode  9; showgui capturemaps"
            guibutton "regen capture"      "mode 10; showgui capturemaps"
            guibutton "ctf"                "mode 11; showgui ctfmaps"
            guibutton "insta ctf"          "mode 12; showgui ctfmaps"
            guibutton "protect"            "mode 13; showgui ctfmaps"
            guibutton "insta protect"      "mode 14; showgui ctfmaps"
        ]
    ]
]

showfileeditor = [
    guinoautotab [
        guieditor $arg1 $arg2 $arg3
        guistayopen [
            guilist [
                guibutton "load" [textfocus @arg1; textload @arg1]
                guibar
                guibutton "save" [textfocus @arg1; textsave @arg1]
                guibar
                guibutton "exec" [textfocus @arg1; textexec]
                guibar
                guibutton "copy" [textfocus @arg1; textcopy]
                guibar
                guibutton "paste" [textfocus @arg1; textpaste]
                guibar
                guibutton "select" [textfocus @arg1; textselectall]
                guibar
                guibutton "clear" [textfocus @arg1; textclear]
            ]
        ]
    ]
]

notepadfile = "untitled.txt"

newgui notepad [
    guifield notepadfile -30
    showfileeditor $notepadfile -80 20
]

notepad = [
    if (> $numargs 0) [notepadfile = $arg1]
    showgui notepad
]

newgui pastebuffer [
    guinoautotab [
        guieditor "#pastebuffer" -80 20
        guistayopen [
            guilist [
                guibutton "exec" [textfocus "#pastebuffer"; textexec]
                guibar
                guibutton "clear" [textfocus "#pastebuffer"; textclear]
            ]
        ]
    ]
]

pastebuffer = [showgui pastebuffer]

newgui scratchpad [
    guinoautotab [
        guieditor "#scratchpad" -80 20
        guistayopen [
            guilist [
                guibutton "exec" [textfocus "#scratchpad"; textexec]
                guibar
                guibutton "copy" [textfocus "#scratchpad"; textcopy]
                guibar
                guibutton "paste" [textfocus "#scratchpad"; textpaste]
                guibar
                guibutton "select" [textfocus "#scratchpad"; textselectall]
                guibar
                guibutton "clear" [textfocus "#scratchpad"; textclear]
            ]
        ]
    ]
]

scratchpad = [showgui scratchpad]
        
newmapsize = 12
savemap_name = temp

newgui editing [
    guibutton "materials.."                           "showgui materials"
    guibutton "toggle edit mode	(key E)"              "edittoggle"
    guibutton "toggle heightmap	(key H)"              ""    
    guibutton "deselect			(key SPACE)"                "cancelsel"
    guibutton "passthrough		(hold ALT)"               "passthrough"
    guibutton "select			drag left mouse   "	          ""
    guibutton "select corners		drag middle mouse "     ""
    guibutton "extend selection	right mouse"          ""
    guibutton "reorient			right mouse"                ""
    guibutton "move selection		drag right mouse"       ""
    guibutton "copy				(key C)"			                  "copy"
    guibutton "paste				(key V)"			                "paste"
    
    guitab ents
    guilist [
      guilist [
        guibutton "drop entities.."              "showgui dropent"
        guibutton "mapmodels.."                  "showgui mapmodels"
        guibutton "light.."                      "resetlight;showgui newlight"
    
        guibutton "newent playerstart   "
        guibutton "newent shells"
        guibutton "newent bullets"
        guibutton "newent rockets"
        guibutton "newent riflerounds"
        guibutton "newent grenades"    
      ]
      guilist [
        guibutton "teleport.."                   "showgui newteleport"
        guibutton "monster.."                    "showgui monsters"
        guibutton "trigger.."                    "showgui triggers"

        guibutton "newent cartridges"    
        guibutton "newent health"
        guibutton "newent healthboost"
        guibutton "newent greenarmour"
        guibutton "newent yellowarmour"
        guibutton "newent quaddamage"
      ]
    ]

    guitab keys    
    guibutton "flip / mirror cube		(key X)"            "flip"
    guibutton "undo					(key Z, U)"                     "undo"
    guibutton "redo					(key I)"                        "redo"
    guibutton "delete				(BACKSPACE)"                  "editdel"
    guibutton "texture palette     	(F2)" "showtexgui"
    guibutton "edit entity gui			(F3)" "showentgui"
    guibutton "mapmodels list			(F4)" "showgui mapmodels"
    guibutton "select all in box		(N)" "entselect insel"
    guibutton "select all matching		(M)" "selentfindall"
    guibutton "center view on ent		(COMMA + scroll)" "entautoview"
    guibutton "edit entity console		(PERIOD)" "selentedit"    
    guibutton "toggle heightmap tex	(MMB in heightmapmode)" ""    
    guicheckbox "toggle outline			(key 7)"        outline
    guicheckbox "toggle wireframe		(key 8)"    wireframe


    guitab scroll
    guitext "push/pull selection			scroll wheel only"
    guitext "push the face of a cube		hold F + scroll wheel"
    guitext "change gridsize				hold G + scroll wheel"
    guitext "change textures			hold Y + scroll wheel"
    guitext "rotate         			hold R + scroll wheel"
    guitext "push corner with cursor		hold Q + scroll wheel"
    guitext "push/pull heightmap			hold LCTRL + scroll wheel"
    guitext "change heightmap brush		hold B + scroll wheel"

    guitab light
    guibutton "recompute lights (slow: 8xAA)" "calclight 1"
    guibutton "recompute lights (quick: no AA, no model shadows)"   "calclight -1"
    guibutton "patch lighting"                         "patchlight"

    guicheckbox "fullbright" fullbright

    guitext "lightprecision (default: 32)"
    guislider lightprecision

    guitext "lighterror (default: 8)"
    guislider lighterror

    //guibutton "lightlod high (6)" "lightlod 6"
    //guibutton "lightlod low  (2)" "lightlod 2"
    //guibutton "lightlod off  (0)" "lightlod 0"


    guitab mapop
    guitext   Savemap:
    guifield  savemap_name 20 [ savemap $savemap_name ]
    guibutton [load @savemap_name map]             "map $savemap_name"    
    guibutton "newmap (default size: 12)"          "newmap $newmapsize"
    guislider "newmapsize" 10 16
    guibutton "increase mapsize (2x)"              "mapenlarge"
    guitext   "max undo size (default: 5 MB)"
    guislider "undomegs" 1 10
    guibutton "optimize geometry"                  "remip"
    guibutton "set map title/author"               "saycommand [/mapmsg ]" 

    guitab cfg
    guitext (mapcfgname)
    showfileeditor (mapcfgname) -50 12
]

newgui materials [
    guibutton "air"                        "editmat air"
    guibutton "water"                      "editmat water"
    guibutton "lava"                       "editmat lava"
    guibutton "clip"                       "editmat clip"
    guibutton "glass"                      "editmat glass"
    guibutton "noclip"                     "editmat noclip"
    guibutton "aiclip"                     "editmat aiclip"
    guibutton "death"                      "editmat death"

    guicheckbox "show material volumes"    showmat

    //guibutton "waterlod very high		(3)"  "waterlod 3"
    //guibutton "waterlod high		(2)"          "waterlod 2"
    //guibutton "waterlod default		(1)"  "waterlod 1"
    //guibutton "waterlod low		(0)"          "waterlod 0"

    //guibutton "watersubdiv very fine	(0)"          "watersubdiv 0"
    //guibutton "watersubdiv fine		(1)"  "watersubdiv 1"
    //guibutton "watersubdiv default		(2)"  "watersubdiv 2"
    //guibutton "watersubdiv coarse		(3)"  "watersubdiv 3"

    //guibutton "water waves on"                     "waterwaves 1"
    //guibutton "water waves off"                    "waterwaves 0"
]

setting_entediting = 1

newgui dropent [
    guitext "drop entities:"
    guiradio "to the floor"                 entdrop 1
    guiradio "on selection"                 entdrop 2
    guiradio "to the floor at selection"    entdrop 3
    guiradio "at current position"          entdrop 0
    guibar
    guicheckbox "entity snap-to-grid"       entselsnap
    guicheckbox "entity selection"          setting_entediting 1 0 [ entediting $setting_entediting ]
]

newgui triggers [
    guibutton [invisible]               [BTmm = -1; showgui trigger2]
    guibutton [a carrot]                [BTmm = 23; showgui trigger2]
    guibutton [switch #1]               [BTmm = 24; showgui trigger2]
    guibutton [switch #2]               [BTmm = 25; showgui trigger2]
    guibutton [door #1]                 [BTmm = 26; showgui trigger2]
    guibutton [door #2]                 [BTmm = 27; showgui trigger2]
    guibutton [door #3]                 [BTmm = 28; showgui trigger2]
]

newgui trigger2 [
    guibutton [animation loop]                      [BTtt = 0; showgui trigger3]
    guibutton [trigger once (popback)]              [BTtt = 1; showgui trigger3]
    guibutton [trigger once (popback & rumble)]     [BTtt = 2; showgui trigger3]
    guibutton [trigger once]                            [BTtt = 3; showgui trigger3]
    guibutton [trigger once (rumble)]               [BTtt = 4; showgui trigger3]
    guibutton [trigger multiple (popback)]          [BTtt = 5; showgui trigger3]
    guibutton [trigger multiple (popback & rumble)] [BTtt = 6; showgui trigger3]
    guibutton [trigger multiple]                        [BTtt = 7; showgui trigger3]
    guibutton [trigger multiple (rumble)]           [BTtt = 8; showgui trigger3]
    guibutton [door (open once)]                        [BTtt = 9; showgui trigger3]
    guibutton [door (auto close)]                   [BTtt = 10; showgui trigger3]
    guibutton [door (locked)]                       [BTtt = 11; showgui trigger3]
    guibutton [vanishing trigger]                   [BTtt = 12; showgui trigger3]
    guibutton [vanishing trigger (rumble)]          [BTtt = 13; showgui trigger3]
    guibutton [End Of Level Trigger]                    [BTtt = 29; showgui trigger3]
]

build_trigger = [newent mapmodel $BTmm $BTtt $BTti]

newgui trigger3 [
    loop i 9 [
        tjt = (concat tag (concatword # $i))
        tjc = (concat BTti "=" $i ";" "build_trigger")
        guibutton $tjt $tjc
    ]
]


resetlight = [
    lightcolor = 0
    lightbright = 1
    lightset 255 255 255
    lightradius = 128
]

lightset = [ 
    lightr = $arg1
    lightg = $arg2
    lightb = $arg3
    if (= $lightbright 0) lightscale
]

lightscale = [
    if (= $lightbright 0) [
        lightr = (div $lightr 2); lightg = (div $lightg 2); lightb = (div $lightb 2)
    ] [
        lightr = (min 255 (* $lightr 2)); lightg = (min 255 (* $lightg 2)); lightb = (min 255 (* $lightb 2))        
    ]
]

lightcmd = [
    result (concat newent light $lightradius $lightr $lightg $lightb)
]

newgui newlight [
    guibutton "sunlight"    "newent light 0 255 200 100"
    guibutton (lightcmd)
    guibar
    guitext "color:"
    guicolor (+ (* (+ (* $lightr 256) $lightg) 256) $lightb)
    guislider lightr 0 255
    guislider lightg 0 255
    guislider lightb 0 255
    guilist [
        guicheckbox "bright"    lightbright 1 0 [lightscale]
        guibar
        guiradio "white"        lightcolour 0 [lightset 255 255 255]
        guiradio "blue"         lightcolour 1 [lightset 192 192 255]
        guiradio "red"          lightcolour 2 [lightset 255 192 192]
        guiradio "green"        lightcolour 3 [lightset 192 255 192]
        guiradio "yellow"       lightcolour 4 [lightset 255 255 192]
        guiradio "purple"       lightcolour 5 [lightset 255 192 255]
        guiradio "turquoise"    lightcolour 6 [lightset 192 255 255]
    ]
    guitext "radius:"
    guislider lightradius 0 512
]

newgui newteleport [
    guibutton "newent teleport 1"
    guibutton "newent teledest 1"
    guibutton "newent teleport 2"
    guibutton "newent teledest 2"
    guibutton "newent teleport 3"
    guibutton "newent teledest 3"
    guibutton "newent teleport 4"
    guibutton "newent teledest 4"
]

newgui mapmodels [
    loop i (nummapmodels) [
        guibutton (mapmodelname $i) (concat newent mapmodel $i)
    ]
]

newgui monsters [
    guibutton "ogro / fireball"      "newent monster 0"
    guibutton "rhino / chaingun"      "newent monster 1"
    guibutton "ratamahatta / shotgun"      "newent monster 2"
    guibutton "slith / rifle" "newent monster 3"
    guibutton "bauul / RL"     "newent monster 4"
    guibutton "hellpig / bite"       "newent monster 5"
    guibutton "knight / iceball" "newent monster 6"
    guibutton "goblin / slimeball"   "newent monster 7"
]

newgui postfx [
    guibutton "(effect OFF)"          "clearpostfx"
    guibutton "bloom (subtle: 30%)"    "bloom 0.3"
    guibutton "bloom (bright: 55%)"    "bloom 0.55"
    guibutton "bloom (intense: 80%)"  "bloom 0.8"
    guibutton "rotoscope" "rotoscope 1"
    guibutton "rotoscope + blur3" "rotoscope 1 1"
    guibutton "rotoscope + blur5" "rotoscope 1 2"
    guibutton "sobel"  "setpostfx sobel"
    guibutton "invert" "setpostfx invert"
    guibutton "gbr"    "setpostfx gbr"
    guibutton "bw"     "setpostfx bw"
    guibutton "blur3"  "setpostfx hblur3; addpostfx vblur3"
    guibutton "blur5"  "setpostfx hblur5; addpostfx vblur5"
]

newgui options [
    guilist [
        guicheckbox "2D menus" gui2d
        guibar
        guicheckbox "apply changes dialog" applydialog
    ]
    guilist [
        guicheckbox "damage screen"     damagescreen
        guibar
        guicheckbox "damage compass"    damagecompass
    ]
	guilist [
    	guicheckbox "hudguns"       hudgun
		if $hudgun [
			guibar
			guicheckbox "sway"		hudgunsway
            guibar
            guicheckbox "muzzle flash" muzzleflash
		]
	]
    guicheckbox "blood"             blood
    guilist [
        guicheckbox "ragdoll deaths"    ragdoll
        if $ragdoll [
            guibar
            guicheckbox "keep after respawn" ragdollmillis 10000
        ]
    ]
    guitext "death velocity multiplier"
    guislider deadpush
    guilist [
        guicheckbox "fullbright player models" fullbrightmodels 50 0
        if $fullbrightmodels [
            guibar
            guiradio "subtle" fullbrightmodels 50
            guibar
            guiradio "bright" fullbrightmodels 100
            guibar
            guiradio "overbright" fullbrightmodels 200
        ]
    ]
    guilist [
        guitext "player model"
        guibar
        guiradio "Mr. Fixit" playermodel 0
        guibar
        guiradio "Ironsnout" playermodel 1 
        guibar
        guiradio "Ogro" playermodel 2
    ]
    guilist [
        guicheckbox "force matching player models" forceplayermodels
        guibar
        guicheckbox "always use team skins" teamskins
    ]
    guilist [
        guicheckbox "outline capture meters" outlinemeters
        guibar 
        guicheckbox "particle text"     particletext
    ]
    guicheckbox "hit sound" hitsound
    guilist [
        guicheckbox "crosshair color effects" crosshairfx
        if $crosshairfx [
            guibar
            guicheckbox "teammates" teamcrosshair
            guibar
            guicheckbox "hits" hitcrosshair 425
        ]
    ]

    guibutton "Restore Defaults" "exec data/defaults.cfg"

    guitab    "gfx"
    guitext "performance key: 0fast~, 2moderate~, 3slow and pretty~"
    guibar
    if (&& (< $shaders 0) (= $renderpath 0)) [
        guicheckbox "2shaders		"			shaders 1 -1
    ] [
		guilist [
        	guicheckbox "2shaders		"           shaders
	    	if $shaders [
            	guibar
            	guiradio "0low detail" shaderdetail 1
            	guibar
            	guiradio "2high detail" shaderdetail 3
	    	]
		]
    ]
    guilist [
        guitext "water		"
        guibar
        guicheckbox "2refraction"  waterrefract
        guibar
        guicheckbox "3reflection"  waterreflect
        guibar
        guicheckbox "0caustics"    caustics
		if (&& (= $renderpath 0) $waterrefract) [
			guibar
        	guicheckbox "0depth fog"   refractfog      
		]
    ]
	if (> $renderpath 0) [
		guilist [
			guitext "waterfalls		"
			guibar
			guicheckbox "2refraction" waterfallrefract
			guibar
			guicheckbox "0reflection" waterfallenv
		]
	]
    if (= $renderpath 0) [
        guilist [
            guicheckbox "3shadow maps	"  shadowmap
            if $shadowmap [
                guibar
                guiradio "2medium quality" shadowmapsize 9 [blurshadowmap 1]
                guibar
                guiradio "3high quality" shadowmapsize 10 [blurshadowmap 2]
            ] [
                guibar
                guicheckbox "0blob shadows" blobs
            ]
        ]
        if (>= $maxtmus 3) [
            guicheckbox "2dynamic lights  "    ffdynlights 5 0
        ]
    ] [
		guilist [
        	guicheckbox "3soft shadows	"  shadowmap
			if $shadowmap [
                guibar
        	    guiradio "2medium quality" shadowmapsize 9 [blurshadowmap 1]
    	        guibar
	            guiradio "3high quality" shadowmapsize 10 [blurshadowmap 2]
			] [
				guibar
				guicheckbox "0blob shadows" blobs
			]
    	]
    	if $glare [
        	glarepreset = 0
        	if (= $glarescale 1) [
            	if (= $blurglare 4) [glarepreset = 1]
            	if (= $blurglare 7) [glarepreset = 3]
        	]
        	if (= $glarescale 2) [
            	if (= $blurglare 3) [glarepreset = 2]
            	if (= $blurglare 7) [glarepreset = 4]
        	]
        	guilist [
            	guicheckbox "3glare			"       glare
            	guibar
            	guiradio "2subtle" glarepreset 1 [blurglare 4; glarescale 1]
            	guibar
            	guiradio "2glowy" glarepreset 2 [blurglare 3; glarescale 2]
            	guibar
            	guiradio "3soft" glarepreset 3 [blurglare 7; glarescale 1]
            	guibar
            	guiradio "3intense" glarepreset 4 [blurglare 7; glarescale 2]
        	]
    	] [
        	guicheckbox "3glare"         glare
    	]
    ]
    guilist [
        guicheckbox "3grass			" grass
		if $grass [
			guibar
			guiradio "2quick fade" grassdist 128
			guibar
			guiradio "2moderate fade" grassdist 256
			guibar
			guiradio "3slow fade" grassdist 512
		]
	]
    if (> $renderpath 0) [
        guilist [
            guicheckbox "0dynamic lights	"    maxdynlights 3 0
            if $maxdynlights [
                guibar
                guiradio "0medium quality" maxdynlights 3
                guibar
                guiradio "2high quality" maxdynlights 5
            ]
        ]
        guilist [
            guicheckbox "0soft explosions	" depthfx
            if $depthfx [
                guibar
                guiradio "0low quality" depthfxsize 7 [depthfxrect 0; depthfxfilter 1; blurdepthfx 1]
                guibar
                guiradio "2medium quality" depthfxsize 10 [depthfxrect 1; depthfxfilter 0; blurdepthfx 0]
                guibar
                guiradio "3high quality" depthfxsize 12 [depthfxrect 1; depthfxfilter 0; blurdepthfx 0]
            ]
        ]
	]
    guicheckbox "0water/lava vertex animation" vertwater
    guicheckbox "0glass reflection"  glassenv
    guilist [
        guicheckbox "0decals		" decals
        if $decals [
            guibar
            guiradio "0quick fade" decalfade 10000 [maxdecaltris 1024]
            guibar
            guiradio "2slow fade" decalfade 60000 [maxdecaltris 4096]
        ]
    ]
    guicheckbox "0fix t-joints (world sparklies)" filltjoints
    guilist [
        guitext "textures		"
        guibar
        guiradio "0low quality" maxtexsize 256
        guibar
        guiradio "2high quality" maxtexsize 0
    ]
    guilist [
        guitext "models		"
        guibar
        guicheckbox "0lighting"    lightmodels
        guibar
        guicheckbox "0reflection"  envmapmodels
        guibar
        guicheckbox "0glow"        glowmodels
        if (> $renderpath 0) [
            guibar
            guicheckbox "2bumpmap" bumpmodels
        ]
    ]
    guilist [
        guitext "animation		"
        guibar
        guiradio "0medium quality" matskel 1
        guibar
        guiradio "0high quality" matskel 0
    ]

    guitab    "display"
    guicheckbox "v-sync"            vsync 1 0
    guicheckbox "fullscreen"        fullscreen
    guitext "gamma (default: 100)"
    guislider gamma
    guitext "full-scene anti-aliasing (default: -1)"
    guilistslider fsaa "-1 0 2 4 8 16"
    guitext "color depth (default: 0)"
    guilistslider colorbits "0 8 16 24 32"
    guitext "z-buffer depth (default: 0)"
    guilistslider depthbits "0 8 16 24 32"
    guitext "stencil bits (default: 1)"
    guislider stencilbits
    guitext "anisotropic filtering (default: 0)"
    guilistslider aniso "0 2 4 8 16"
	guilist [
    	guicheckbox "bilinear filtering" bilinear
		guibar
		guicheckbox "trilinear filtering (mipmaps)" trilinear
	]

    guitab   "res"
    guitext "field of view (default: 100)"
    guislider fov
    guistayopen [
      guilist [
        guilist [
            guibutton "320x240"              "screenres 320 240" (if (&& (= $scr_w 320) (= $scr_h 240)) [result "radio_on"] [result "radio_off"])
            guibutton "640x480"              "screenres 640 480" (if (&& (= $scr_w 640) (= $scr_h 480)) [result "radio_on"] [result "radio_off"])
            guibutton "800x600"              "screenres 800 600" (if (&& (= $scr_w 800) (= $scr_h 600)) [result "radio_on"] [result "radio_off"])
            guibutton "1024x768"             "screenres 1024 768" (if (&& (= $scr_w 1024) (= $scr_h 768)) [result "radio_on"] [result "radio_off"])
            guibutton "1152x864"             "screenres 1152 864" (if (&& (= $scr_w 1152) (= $scr_h 864)) [result "radio_on"] [result "radio_off"])
            guibutton "1280x960"             "screenres 1280 960" (if (&& (= $scr_w 1280) (= $scr_h 960)) [result "radio_on"] [result "radio_off"])
            guibutton "1400x1050"            "screenres 1400 1050" (if (&& (= $scr_w 1400) (= $scr_h 1050)) [result "radio_on"] [result "radio_off"])
            guibutton "1600x1200"            "screenres 1600 1200" (if (&& (= $scr_w 1600) (= $scr_h 1200)) [result "radio_on"] [result "radio_off"])
            guibutton "1792x1344"            "screenres 1792 1344" (if (&& (= $scr_w 1792) (= $scr_h 1344)) [result "radio_on"] [result "radio_off"])
            guibutton "1856x1392"            "screenres 1856 1392" (if (&& (= $scr_w 1856) (= $scr_h 1392)) [result "radio_on"] [result "radio_off"])
            guibutton "1920x1440"            "screenres 1920 1440" (if (&& (= $scr_w 1920) (= $scr_h 1440)) [result "radio_on"] [result "radio_off"])
            guibutton "2048x1536"            "screenres 2048 1536" (if (&& (= $scr_w 2048) (= $scr_h 1536)) [result "radio_on"] [result "radio_off"])
        ]
		guibar
        guilist [
            guibutton "1024x640"             "screenres 1024 640" (if (&& (= $scr_w 1024) (= $scr_h 640)) [result "radio_on"] [result "radio_off"])
            guibutton "1280x720"             "screenres 1280 720" (if (&& (= $scr_w 1280) (= $scr_h 720)) [result "radio_on"] [result "radio_off"])
            guibutton "1280x768"             "screenres 1280 768" (if (&& (= $scr_w 1280) (= $scr_h 768)) [result "radio_on"] [result "radio_off"])
            guibutton "1280x800"             "screenres 1280 800" (if (&& (= $scr_w 1280) (= $scr_h 800)) [result "radio_on"] [result "radio_off"])
            guibutton "1440x900"             "screenres 1440 900" (if (&& (= $scr_w 1440) (= $scr_h 900)) [result "radio_on"] [result "radio_off"])
            guibutton "1600x1000"            "screenres 1600 1000" (if (&& (= $scr_w 1600) (= $scr_h 1000)) [result "radio_on"] [result "radio_off"])
            guibutton "1680x1050"            "screenres 1680 1050" (if (&& (= $scr_w 1680) (= $scr_h 1050)) [result "radio_on"] [result "radio_off"])
            guibutton "1920x1080"            "screenres 1920 1080" (if (&& (= $scr_w 1920) (= $scr_h 1080)) [result "radio_on"] [result "radio_off"])
            guibutton "1920x1200"            "screenres 1920 1200" (if (&& (= $scr_w 1920) (= $scr_h 1200)) [result "radio_on"] [result "radio_off"])
            guibutton "2048x1280"            "screenres 2048 1280" (if (&& (= $scr_w 2048) (= $scr_h 1280)) [result "radio_on"] [result "radio_off"])
            guibutton "2560x1600"            "screenres 2560 1600" (if (&& (= $scr_w 2560) (= $scr_h 1600)) [result "radio_on"] [result "radio_off"])
            guibutton "3840x2400"            "screenres 3840 2400" (if (&& (= $scr_w 3840) (= $scr_h 2400)) [result "radio_on"] [result "radio_off"])
        ]
      ]
    ]

    guitab   "sound"
    guitext "sound volume"
    guislider soundvol
    guitext "music volume"
    guislider musicvol
    guitext "sound channels"
    guislider soundchans
    guitext "sound frequency"
    guilistslider soundfreq "11025 22050 44100"
    guitext "sound buffer length"
    guislider soundbufferlen
    guicheckbox "Mumble positional audio" mumble

    guitab  "input"
    guicheckbox "invert mouse"      invmouse

    guilist [
        guitext "mouse sensitivity: "
        newsens = $sensitivity
        guifield newsens 8 [sensitivity $newsens]
    ]

    guibar
    guitext "(basic keybinds, for anything more use the 'bind' command)"
    guitext "select action to bind and press desired keys (ESC when done):"

    guilistsplit n 2 $bindactions [
        newbinds = (concatword "newbinds" $i)
        guilist [
            guitext (tabify [@@(concatword $n ": ")] 4)
            @newbinds = (searchbinds [@@n])
            guikeyfield @newbinds -8 [
                oldbinds = (searchbinds [@@@n])
                loop i (listlen $oldbinds) [bind (at $oldbinds $i) ""]
                loop i (listlen $@@newbinds) [bind (at $@@@newbinds $i) [@@@@n]]
            ]
        ]
    ] [guibar]

    guitab "console"
    guicheckbox "chat console" miniconfilter 0x300 0 
    guitext "console size (lines)"
    guislider consize 
    guitext "console filter:"
    guilist [
        guilist [
            guibitfield "important information" confilter 0x01  
            guibitfield "warnings" confilter 0x02
            guibitfield "errors" confilter 0x04
            guibitfield "init messages" confilter 0x10
            guibitfield "script messages" confilter 0x20
	    ]
        guibar
        guilist [
            guibitfield "chat" confilter 0x100
            guibitfield "team chat" confilter 0x200
            guibitfield "game events" confilter 0x400
            guibitfield "self frags" confilter 0x800
            guibitfield "other player frags" confilter 0x1000
        ]
    ]
    guitext "full console size (percent of screen)"
    guislider fullconsize
    guitext "full console filter:"
    guilist [
        guilist [
            guibitfield "important information" fullconfilter 0x01
            guibitfield "warnings" fullconfilter 0x02
            guibitfield "errors" fullconfilter 0x04
            guibitfield "init messages" fullconfilter 0x10
            guibitfield "script messages" fullconfilter 0x20
        ]
        guibar
        guilist [
            guibitfield "chat" fullconfilter 0x100
            guibitfield "team chat" fullconfilter 0x200
            guibitfield "game events" fullconfilter 0x400
            guibitfield "self frags" fullconfilter 0x800
            guibitfield "other player frags" fullconfilter 0x1000
        ]
    ]

    guitab "autoexec.cfg"
    guitext "autoexec.cfg"
    showfileeditor "autoexec.cfg" -60 15
]

bindactions = [forward backward left right jump attack togglezoom saycommand sayteamcommand showscores toggleconsole "setweapon 0" "setweapon 1" "setweapon 2" "setweapon 3" "setweapon 4" "setweapon 5" "setweapon 6" "weapon" "universaldelta 1" "universaldelta -1" edittoggle]

entupdate = [ entset $tmpt $tmp0 $tmp1 $tmp2 $tmp3 ]

initentgui = [
  tmpt = ( et )
  tmp0 = ( ea 0 )
  tmp1 = ( ea 1 )
  tmp2 = ( ea 2 )
  tmp3 = ( ea 3 )
]
    
genentattributes = [
    entattributes = ""
    n = ( listlen $arg2 )
    loop i $n [       
        do [
          t@i = [ guitext @(at $arg2 $i) ]
          a@i = [ guislider tmp@i @(at $arg3 (* 2 $i)) @(at $arg3 (+ 1 (* 2 $i))) entupdate ]
          entattributes = [ @@entattributes @t@@i; @a@@i; ]
        ]
    ]
]

guilistsplit = [
  guilist [
    i = 0
    n = (listlen $arg3)
    z = (div (+ $n (- $arg2 1)) $arg2)
    loop a $arg2 [
      guilist [
        t = (min (+ $i $z) $n)
        while [< $i $t] [
          push $arg1 (at $arg3 $i)
          @@@arg4
          pop $arg1
          i = (+ $i 1)
        ]
      ]
      if (&& (>= $numargs 5) (< (+ $a 1) $arg2)) [@arg5]
    ]
  ]
]

quickeditmenu = [
  guitext "Quick Commands:"
  guibar
  guifield  savemap_name 10 [ savemap $savemap_name ]
  guibutton "quick light"       "calclight -1"
  guibutton "optimize map"      "remip"
  guibutton "new entity"        "newent shells"
  guibar
  guibutton newmap
  guibar
  guibutton help "showgui editing"
]

matmenu = [
  guibutton "air"                        "editmat air"
  guibutton "water"                      "editmat water"
  guibutton "clip"                       "editmat clip"
  guibutton "glass"                      "editmat glass"
  guibutton "noclip"                     "editmat noclip"
  guibutton "lava"                       "editmat lava"
  guibutton "death"                      "editmat death"
  guibar
  guicheckbox "show material volumes"    showmat
]

brushmenu = [
  guilist [ // diamonds
  guiimage "packages/icons/brush_1c.png" brush_0
  guiimage "packages/icons/brush_21c.png" brush_1
  guiimage "packages/icons/brush_421c.png" brush_2
  ]
  guilist [ // squares
  guiimage "packages/icons/brush_3s.png" brush_3
  guiimage "packages/icons/brush_5s.png" brush_4
  guiimage "packages/icons/brush_7s.png" brush_5
  ]
  guititle smooth
  guilist [ // smooth
  guiimage "packages/icons/brush_3s.png" brush_6
  guiimage "packages/icons/brush_5s.png" brush_7
  guiimage "packages/icons/brush_7s.png" brush_8
  ]
]

newentgui = [
  do [
    genentattributes [@@arg1] [@@arg2] [@@arg3]
    newgui @arg1 [
      guitext $tmpt
      guibar
      @entattributes
      guitab type 
      guilistsplit n 2 $enttypelist [
          guibutton $n [ entset @n ]       
      ]  
      guitab misc
      @quickeditmenu
    ]
  ]
]

loop i $enttypelength [
  newentgui (at $enttypelist $i) "" ""
]

newgui materials [
  @matmenu
  guitab misc
  @quickeditmenu
]

newgui brushes [
  @brushmenu
  guitab misc
  @quickeditmenu
]

newgui quickedit [
  @quickeditmenu
  guitab materials
  @matmenu
]

newentgui light "radius red green blue" "0 400 0 255 0 255 0 255"
newentgui spotlight "radius" "0 200"
newentgui playerstart "direction" "0 360"
newentgui teleport "tag" "0 20"
newentgui teledest "direction tag" "0 360 0 20"
newentgui monster "direction type" "0 360 0 7"
newentgui mapmodel "direction model" "0 360 0 100"
newentgui envmap "radius" "0 400"
newentgui jumppad "Z Y X" "0 200 0 200 0 200"
newentgui sound "type radius size" "0 20 0 500 0 500"
newentgui particles "type" "0 3"

contexteditgui = [ 
  if ($hmapedit) [
    showgui brushes
  ] [
    if (enthavesel) [
      initentgui; 
      do [ showgui (et) ] 
    ] [
      if (havesel) [
        showgui materials
      ] [
        showgui quickedit
      ]
    ]
  ]
]

showentgui = [ do [ contexteditgui ] ] // legacy bind





